plugins {
    id 'java'
    id 'eclipse'
    id 'maven-publish'
    id 'net.neoforged.gradle' version '[6.0.18,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = mod_version
group = mod_group_id
archivesBaseName  = mod_id

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

minecraft {
    mappings channel: mapping_channel, version: mapping_version

    runs {
        client {
            workingDirectory file('run')
            property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods { mod_id { source sourceSets.main } }
        }
        server {
            workingDirectory file('run')
            mods { mod_id { source sourceSets.main } }
        }
        data {
            workingDirectory file('run')
            property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            args '--mod', mod_id, '--all', '--output', file("src/main/generated"), '--existing', file("src/main/resources")
            mods { mod_id { source sourceSets.main } }
        }
    }
}

sourceSets.main.resources { srcDir 'src/main/generated' }

repositories {
    // Curse Forge
    maven { url "https://www.cursemaven.com" }
    maven { //JEI
		name = "Jared's maven"
		url = "https://maven.blamejared.com/"
 	}
 	//Curios
 	maven { url = "https://maven.theillusivec4.top/" }
 	//ExpandAbility
    maven {
        url "https://maven.florens.be/releases"
        content {
            includeGroup "be.florens"
        }
    }
    maven { url = "https://jitpack.io" }
}

dependencies {
    minecraft "net.neoforged:forge:${minecraft_version}-${neo_version}"

    implementation fg.deobf("be.florens:expandability-forge:9.0.4")
    compileOnly(annotationProcessor("com.github.LlamaLad7:MixinExtras:0.1.1"))

    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}")
  	compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}")

  	compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")

    runtimeOnly fg.deobf("curse.maven:theoneprobe-${curseforge_theoneprobe}")
}

processResources {
    inputs.property 'mod_version', mod_version
    inputs.property 'minecraft_version', minecraft_version

    filesMatching('META-INF/mods.toml') {
        expand 'file': ['jarVersion': mod_version],
                'minecraft_version': minecraft_version,
                'neo_version': neo_version,
                'lang_version': neo_version.split('\\.')[0]
    }
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : "relics",
                "Specification-Vendor"    : "relics",
                "Specification-Version"   : "0.3", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${relics_version}",
                "Implementation-Vendor"   : "relics",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy 'reobfJar'
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}